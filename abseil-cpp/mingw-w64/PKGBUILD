pkgname=mingw-w64-abseil-cpp
pkgver=20250512.0
pkgrel=1
pkgdesc='Collection of C++ library code designed to augment the C++ standard library (mingw-w64)'
arch=('any')
url='https://abseil.io'
license=('Apache-2.0')
depends=('mingw-w64-crt' 'mingw-w64-winpthreads')
makedepends=('mingw-w64-cmake-static')
options=('!buildflags' '!strip' 'staticlibs')
source=("https://github.com/abseil/abseil-cpp/archive/$pkgver/abseil-cpp-$pkgver.tar.gz")
sha256sums=('7262daa7c1711406248c10f41026d685e88223bc92817d16fb93c19adb57f669')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd abseil-cpp-$pkgver
  for _arch in ${_architectures}; do
    ${_arch}-cmake -B build-${_arch} .
    cmake --build build-${_arch}
    ${_arch}-cmake-static -B build-static-${_arch} -DCMAKE_INSTALL_PREFIX:PATH=/usr/$_arch/static .
    cmake --build build-static-${_arch}
  done
}

package() {
  cd abseil-cpp-$pkgver
  for _arch in $_architectures; do
    DESTDIR=$pkgdir cmake --install build-${_arch}
    DESTDIR=$pkgdir cmake --install build-static-${_arch}
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a "$pkgdir"/usr/${_arch}/static/lib/*.a
  done
}
