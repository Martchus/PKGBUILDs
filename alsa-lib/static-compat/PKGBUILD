_pkgname=alsa-lib
pkgname=static-compat-$_pkgname
pkgver=1.1.3  # intentionally outdated for compatibility
pkgrel=1
epoch=1
pkgdesc="An alternative implementation of Linux sound support"
arch=(x86_64)
url="https://www.alsa-project.org"
license=(LGPL-2.1-or-later)
depends=('gcc-libs-static-compat')
makedepends=('static-compat-configure')
source=(
  https://www.alsa-project.org/files/pub/lib/$_pkgname-$pkgver.tar.bz2{,.sig}
  $_pkgname-1.2.10-config_header.patch::https://github.com/alsa-project/alsa-lib/commit/0e3dfb9f705ca78be34cd70fd59d67c431e29cc7.patch
)
sha512sums=('f5dbe2375a8c66af14378314a5238284d4ed63dfc86a750c0c6e8f6cdb6b1ea2d8ef26f870b5d152dc0b77d9b40821cab523f6734902b91583beb08e28c66850'
            'SKIP'
            '4c10050fb92da9c61e08eb742b60245314362bf8b7f5c34d5edade2f0bcaa0930c1d30267db0a95de2391c01a4aa59f5565423a1c41e42471f3dc9614b4d9b8a')
b2sums=('365f0cadbdc61b033b747bda3b5fc99504ffd057419b74683ef6a154496817059580bb4a495b1291eccc24efa5394f1c359fec3ca8c4a8dab88250bd8e8eb0fd'
        'SKIP'
        '73ce802cfcefed592d3820e4e8fdc2cfcb0666e60e07ede87f5581acf751e2c7552bc9347c1075552337047ec4c2cb64dbc92433d4e4b2e52cf6cad36f2679f2')
validpgpkeys=('F04DF50737AC1A884C4B3D718380596DA6E59C91') # ALSA Release Team (Package Signing Key v1) <release@alsa-project.org>

prepare() {
  source static-compat-environment
  cd $_pkgname-$pkgver
  autoreconf -fiv
}

build() {
  source static-compat-environment
  # -flto=auto is not supported: https://github.com/alsa-project/alsa-lib/issues/110
  CFLAGS+=" -flto-partition=none"
  cd $_pkgname-$pkgver
  # building shared libary as alsa-lib cannot be bundled due to the plugin system (at least that is my assumption
  # considering using a static build leads to crashes)
  static-compat-configure --prefix=/usr --enable-shared --disable-static --without-debug --disable-python
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  source static-compat-environment
  export LD_LIBRARY_PATH="$_pkgname-$pkgver/src/.libs/:$LD_LIBRARY_PATH"
  make -k check -C $_pkgname-$pkgver || true
}

package() {
  source static-compat-environment
  make DESTDIR="$pkgdir" install -C $_pkgname-$pkgver
  rm -r $pkgdir/usr/{bin,share/alsa,share/aclocal}
}
