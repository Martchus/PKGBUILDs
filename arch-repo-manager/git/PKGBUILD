# Maintainer: Martchus <martchus@gmx.net>

# All my PKGBUILDs are managed at https://github.com/Martchus/PKGBUILDs where
# you also find the URL of a binary repository.

_reponame=arch-repo-manager
pkgname=arch-repo-manager-git
_name=${pkgname%-git}
pkgver=78.d920368
pkgrel=2
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
pkgdesc='Tool for managing custom Arch Linux repositories'
license=('GPL')
depends=('c++utilities-git' 'boost-libs' 'libarchive')
optdepends=('reflective-rapidjson-git: development with libpkg/librepomgr'
            "$_name-doc: API documentation")
makedepends=('cmake' 'git' 'ninja' 'boost' 'rapidjson-git' 'reflective-rapidjson-git')
checkdepends=('cppunit')
options=(!strip)
url="https://github.com/Martchus/${_reponame}"
source=("${_reponame}::${MARTCHUS_GIT_URL_PREFIX_PRIVATE:-git://github.com/Martchus}/${_reponame}.git"
        'tabulate::git://github.com/p-ranav/tabulate.git')
sha256sums=('SKIP' 'SKIP')

pkgver() {
  cd "$srcdir/${PROJECT_DIR_NAME:-$_reponame}"
  echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/${PROJECT_DIR_NAME:-$_reponame}"

  rm -r 3rdparty/tabulate
  ln -sr ../tabulate 3rdparty/tabulate
}

build() {
  cd "$srcdir/${PROJECT_DIR_NAME:-$_reponame}"
  cmake \
    -G Ninja \
    -DCMAKE_BUILD_TYPE:STRING='RelWithDebInfo' \
    -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
    -DCONFIGURATION_NAME:STRING='git' \
    -DCONFIGURATION_PACKAGE_SUFFIX:STRING='-git' \
    -DCONFIGURATION_TARGET_SUFFIX:STRING='git' \
    -DBUILD_SHARED_LIBS:BOOL=ON \
    -DDUMMY_BUILD_ACTION_ENABLED:BOOL=ON \
    -DREFLECTION_GENERATOR_EXECUTABLE:FILEPATH='/usr/bin/reflective_rapidjson_generator-git' \
    .
  VERBOSE=1 ninja
}

check() {
  cd "$srcdir/${PROJECT_DIR_NAME:-$_reponame}"
  ninja check
}

package() {
  cd "$srcdir/${PROJECT_DIR_NAME:-$_reponame}"
  DESTDIR="${pkgdir}" ninja install
}
