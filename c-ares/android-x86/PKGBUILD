_pkgname=c-ares
_android_arch=x86
pkgname=android-$_android_arch-$_pkgname
pkgver=1.34.5
pkgrel=1
pkgdesc="A C library for asynchronous DNS requests (Android, $_android_arch)"
arch=(x86_64)
url="https://c-ares.org/"
_url=https://github.com/c-ares/c-ares
license=(MIT)
depends=(android-ndk)
makedepends=(
  android-cmake
  git
)
options=(!strip !buildflags !libtool staticlibs !emptydirs)
source=(
  git+$_url.git?signed#tag=v$pkgver
)
sha512sums=('5703fb5153e6c4d026e7fcfe63a3626c1fb1464d10642f4b5657368eb0b0bf17dab949a84ca4c3fa3053c1b0013588bd6de23bea9c3eaf2fc1cece4eeff1c67c')
b2sums=('6c50af22f564c3a9ef5c16f1f383a4a632bf32844d6b19bc1cda7a6a418f473ebead447420d744d0cb3f9b1e472fab95322c87837e0ad54e966e869a2192af7f')
validpgpkeys=(
  27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2  # Daniel Stenberg <daniel@haxx.se>
  DA7D64E4C82C6294CB73A20E22E3D13B5411B7CA  # Brad House <brad@brad-house.com>
)

build() {
  source android-env ${_android_arch}
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -S $_pkgname
    -W no-dev
    -D CARES_STATIC=ON
    -D CARES_SHARED=ON
  )
  android-$_android_arch-cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  source android-env ${_android_arch}
  ctest --test-dir build --output-on-failure
}

package() {
  source android-env ${_android_arch}
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $_pkgname/LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $_pkgname/{AUTHORS,{CONTRIBUTING,README,RELEASE-NOTES}.md} -t "$pkgdir/usr/share/doc/$pkgname/"
  rm -rf "${pkgdir}/${ANDROID_PREFIX_SHARE}"
  ${ANDROID_STRIP} -g --strip-unneeded "${pkgdir}/${ANDROID_PREFIX_LIB}"/*.so
  ${ANDROID_STRIP} -g "${pkgdir}/${ANDROID_PREFIX_LIB}"/*.a
}
