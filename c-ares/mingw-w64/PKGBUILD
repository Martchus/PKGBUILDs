pkgname=mingw-w64-c-ares
_pkgname=c-ares
_protover=30.1
pkgver=1.34.6
pkgrel=1
pkgdesc="A C library for asynchronous DNS requests (mingw-w64)"
arch=('any')
url="https://c-ares.org/"
_url=https://github.com/c-ares/c-ares
license=(MIT)
depends=(mingw-w64-crt)
makedepends=(mingw-w64-cmake-static git ninja)
options=(!strip !buildflags staticlibs)
source=(
  git+$_url.git?signed#tag=v$pkgver
)
sha512sums=('31b306dde129efa6c9e063d456a0941517fbf9ed95f3e1442ace41f31f5214cdcebd4dba6113c6de317d4ce91027d06fe13347470bb08d368274068c43a0902f')
b2sums=('0389469a2732385be87c841f95f719e2c20c98d78fe1c68b7588f2241dc3f57aa2bd5bbdd3f8b468fde4fe332179449ad67c5ef0bacd0e9fe210275344be7186')
validpgpkeys=(
  27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2  # Daniel Stenberg <daniel@haxx.se>
  DA7D64E4C82C6294CB73A20E22E3D13B5411B7CA  # Brad House <brad@brad-house.com>
)

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  local flags=(
    -GNinja
    -S "$_pkgname"
    -DCMAKE_DLL_NAME_WITH_SOVERSION=ON
    -Wno-dev
  )
  for _arch in ${_architectures}; do
    ${_arch}-cmake -B build-${_arch} "${flags[@]}" \
      -DCARES_STATIC=OFF -DCARES_SHARED=ON \
      .
    cmake --build build-${_arch}
    ${_arch}-cmake-static -B build-static-${_arch} "${flags[@]}" \
      -DCMAKE_INSTALL_PREFIX:PATH=/usr/$_arch/static \
      -DCARES_STATIC=ON -DCARES_SHARED=OFF \
      .
    cmake --build build-static-${_arch}
  done
}

package() {
  for _arch in ${_architectures}; do
    DESTDIR=$pkgdir cmake --install build-${_arch}
    DESTDIR=$pkgdir cmake --install build-static-${_arch}
    rm -vr "$pkgdir"/usr/${_arch}/share/man
    rm -vr "$pkgdir"/usr/${_arch}/static/share/man
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a "$pkgdir"/usr/${_arch}/static/lib/*.a
  done
  install -vDm 644 $_pkgname/LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname/"
}
