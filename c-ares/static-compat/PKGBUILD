_pkgname=c-ares
pkgname=static-compat-$_pkgname
pkgver=1.34.6
pkgrel=1
pkgdesc="A C library for asynchronous DNS requests"
arch=(x86_64)
url="https://c-ares.org/"
_url=https://github.com/c-ares/c-ares
license=(MIT)
depends=(gcc-static-compat)
makedepends=(
  static-compat-cmake
  git
)
source=(
  git+$_url.git?signed#tag=v$pkgver
)
sha512sums=('31b306dde129efa6c9e063d456a0941517fbf9ed95f3e1442ace41f31f5214cdcebd4dba6113c6de317d4ce91027d06fe13347470bb08d368274068c43a0902f')
b2sums=('0389469a2732385be87c841f95f719e2c20c98d78fe1c68b7588f2241dc3f57aa2bd5bbdd3f8b468fde4fe332179449ad67c5ef0bacd0e9fe210275344be7186')
validpgpkeys=(
  27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2  # Daniel Stenberg <daniel@haxx.se>
  DA7D64E4C82C6294CB73A20E22E3D13B5411B7CA  # Brad House <brad@brad-house.com>
)

build() {
  source static-compat-environment
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -S $_pkgname
    -W no-dev
    -D CARES_STATIC=ON
    -D CARES_SHARED=OFF
  )
  static-compat-cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  source static-compat-environment
  ctest --test-dir build --output-on-failure
}

package() {
  source static-compat-environment
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $_pkgname/LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $_pkgname/{AUTHORS,{CONTRIBUTING,README,RELEASE-NOTES}.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
