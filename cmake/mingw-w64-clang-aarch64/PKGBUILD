pkgname=mingw-w64-clang-aarch64-cmake
pkgver=1
pkgrel=1
arch=('any')
pkgdesc="CMake wrapper for MinGW (mingw-w64)"
depends=('cmake' 'mingw-w64-clang-aarch64-pkg-config' 'mingw-w64-clang-aarch64-libc++' 'mingw-w64-clang-environment')
optdepends=(
  'mingw-w64-clang-aarch64-binutils: windmc support'
  'mingw-w64-clang-aarch64-wine: runtime support'
)
license=("GPL")
url="http://fedoraproject.org/wiki/MinGW"
source=("mingw-cmake.sh"
        "toolchain-mingw.cmake")
sha256sums=('5cc1682d0ff3c86feb476167bc3530c0b8f31193ccc174dd7191740a0c7e24c9'
            'e9272994067d2a01cb65cd42357b944ad68bc39714662954d473d861751251b0')

_architectures="aarch64-w64-mingw32"

build() {
  for _arch in ${_architectures}; do
    sed "s|@TRIPLE@|${_arch}|g;s|@PROCESSOR@|${_arch::-12}|g" toolchain-mingw.cmake > toolchain-${_arch}.cmake
    sed "s|@TRIPLE@|${_arch}|g" mingw-cmake.sh > ${_arch}-cmake
  done
}

package() {
  install -d "${pkgdir}"/usr/bin
  install -d "${pkgdir}"/usr/share/mingw
  for _arch in ${_architectures}; do
    install -m 644 toolchain-${_arch}.cmake "${pkgdir}"/usr/share/mingw/
    install -m 755 ${_arch}-cmake "${pkgdir}"/usr/bin/
  done
}

