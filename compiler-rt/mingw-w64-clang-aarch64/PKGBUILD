pkgname=mingw-w64-clang-aarch64-compiler-rt
pkgver=21.1.3
pkgrel=1
pkgdesc="Compiler runtime libraries for clang (mingw-w64)"
arch=('any')
url="https://compiler-rt.llvm.org/"
license=('Apache-2.0 WITH LLVM-exception')
depends=('glibc' 'libstdc++' 'libgcc' 'clang' 'mingw-w64-clang-aarch64-headers' 'mingw-w64-clang-aarch64-crt' 'mingw-w64-clang-aarch64-winpthreads')
makedepends=('cmake' 'ninja' 'python' 'lld' 'llvm')
options=('!buildflags' 'staticlibs' '!strip')
_source_base=https://github.com/llvm/llvm-project/releases/download/llvmorg-$pkgver
source=($_source_base/compiler-rt-$pkgver.src.tar.xz{,.sig}
        $_source_base/cmake-$pkgver.src.tar.xz{,.sig}
        $_source_base/llvm-$pkgver.src.tar.xz{,.sig}
        $_source_base/third-party-$pkgver.src.tar.xz{,.sig})
sha256sums=('143b6ac5278788a9010f2464e77627945c1633706bb571b664e14a5350670d93'
            'SKIP'
            '4db6f028b6fe360f0aeae6e921b2bd2613400364985450a6d3e6749b74bf733a'
            'SKIP'
            'a80f2dbfa24a0c4d81089e6245936dcd0c662c90f643d1706bb44e7bc8338ff1'
            'SKIP'
            '2bae76a7c7db4096b921589ae94c030727ee0dcb600bfe40353878937af61aa0'
            'SKIP')
validpgpkeys=('474E22316ABF4785A88C6E8EA2C794A986419D8A'  # Tom Stellard <tstellar@redhat.com>
              'D574BD5D1D0E98895E3BF90044F2485E45D59042'  # Tobias Hieta <tobias@hieta.se>
              'FFB3368980F3E6BB5737145A316C56D064CACBA5'  # Douglas Yung <douglas.yung@sony.com>
)

_targets="aarch64-w64-mingw32"

prepare() {
  mv cmake{-$pkgver.src,}
  mv llvm{-$pkgver.src,}
  mv third-party{-$pkgver.src,}
}

build() {
  cd compiler-rt-$pkgver.src

  for _target in ${_targets}; do
    export CC=clang CXX=clang++ ASM=clang AR=llvm-ar RANLIB=llvm-ranlib DLLTOOL=llvm-dlltool
    export ASMFLAGS="-rtlib=compiler-rt -fuse-ld=lld -mguard=cf -target $_target -Xclang -triple -Xclang $_target"
    export CFLAGS="-rtlib=compiler-rt -fuse-ld=lld -mguard=cf -target $_target -Xclang -triple -Xclang $_target"
    export CXXFLAGS="-rtlib=compiler-rt -fuse-ld=lld -mguard=cf -stdlib=libc++ -target $_target -Xclang -triple -Xclang $_target"

    local llvmtarget=${_target%%-*}
    local cmake_args=(
        -G Ninja
        -B build-${_target}
        -DCMAKE_BUILD_TYPE=Release
        -DCMAKE_CROSSCOMPILING=YES
        -DCMAKE_SYSTEM_NAME=Windows
        -DCMAKE_SYSTEM_PROCESSOR=${_target%%-*}
        -DCMAKE_MODULE_PATH="$srcdir/llvm/cmake/modules;$srcdir/cmake/Modules"
        -DCMAKE_INSTALL_PREFIX=/usr/$_target
        -DCMAKE_FIND_ROOT_PATH=/usr/$_target
        -DCMAKE_FIND_ROOT_PATH_MODE_INCLUDE=ONLY
        -DCMAKE_FIND_ROOT_PATH_MODE_PACKAGE=ONLY
        -DCMAKE_FIND_ROOT_PATH_MODE_LIBRARY=ONLY
        -DCMAKE_SKIP_RPATH=ON
        -DCMAKE_C_COMPILER_TARGET=$_target
        -DCMAKE_C_COMPILER_WORKS=1
        -DCMAKE_CXX_COMPILER_WORKS=1
        -DLLVM_DIR="$srcdir"/llvm
        -DSANITIZER_CXX_ABI=libc++
        -DCOMPILER_RT_OS_DIR=windows
        -DCOMPILER_RT_BUILD_LIBFUZZER=OFF
        -DCOMPILER_RT_BUILD_PROFILE=OFF
        -DCOMPILER_RT_BUILD_SANITIZERS=OFF
        -DCOMPILER_RT_BUILD_BUILTINS=ON
        -DCOMPILER_RT_BUILD_XRAY=OFF
        -DCOMPILER_RT_DEFAULT_TARGET_ONLY=ON
        -DCOMPILER_RT_USE_BUILTINS_LIBRARY=ON
        -DCOMPILER_RT_EXCLUDE_ATOMIC_BUILTIN=FALSE
        -DCOMPILER_RT_INSTALL_PATH=/usr/lib/clang/${pkgver%%.*}
    )
    cmake "${cmake_args[@]}"
    cmake --build build-${_target} --verbose
  done
}

package() {
  cd compiler-rt-$pkgver.src

  for _target in ${_targets}; do
    DESTDIR="$pkgdir" cmake --install build-${_target}
    rm -vr "$pkgdir"/usr/lib/clang/${pkgver%%.*}/include
  done
  install -Dm644 LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
