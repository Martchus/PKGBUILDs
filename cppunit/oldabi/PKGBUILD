# $Id$
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>

# All my PKGBUILDs are managed at https://github.com/Martchus/PKGBUILDs where
# you also find the URL of a binary repository.

_name=cppunit
pkgname=cppunit-oldabi
pkgver=1.13.2
pkgrel=1
pkgdesc="A C++ unit testing framework (using the old C++ ABI)"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/cppunit"
license=('LGPL')
depends=('sh' 'glibc' 'libstdc++' 'libgcc')
conflicts=($_name)
options=('!emptydirs')
source=(http://dev-www.libreoffice.org/src/${_name}-${pkgver}.tar.gz)
md5sums=('d1c6bdd5a76c66d2c38331e2d287bc01')

build() {
   cd ${_name}-${pkgver}
   export CXXFLAGS="$CXXFLAGS -D_GLIBCXX_USE_CXX11_ABI=0"
   ./configure --prefix=/usr --disable-static
   make
}

check() {
  cd ${_name}-${pkgver}
  make check
}

package() {
   cd ${_name}-${pkgver}
   make DESTDIR="${pkgdir}" install
}
