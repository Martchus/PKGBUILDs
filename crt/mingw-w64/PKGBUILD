# Maintainer of official package: Felix Yan <felixonmars@archlinux.org>

pkgname=mingw-w64-crt
pkgver=12.0.0
pkgrel=4
pkgdesc='MinGW-w64 CRT for Windows'
arch=('any')
url='https://www.mingw-w64.org'
license=('LicenseRef-custom')
groups=('mingw-w64-toolchain' 'mingw-w64')
makedepends=("mingw-w64-headers=${pkgver}")
[[ $pkgname =~ .*-clang-.* ]] && makedepends+=('mingw-w64-configure') || makedepends+=('mingw-w64-gcc' 'mingw-w64-binutils')
options=('!strip' '!buildflags' 'staticlibs' '!emptydirs')
validpgpkeys=('CAF5641F74F7DFBA88AE205693BDB53CD4EBC740')
source=(https://sourceforge.net/projects/mingw-w64/files/mingw-w64/mingw-w64-release/mingw-w64-v${pkgver}.tar.bz2{,.sig})
sha512sums=('949b2bfab8763ab10ec4e9fdfdaf5361517a4ab787fb98ab419b38d02694061c2e821ebbf6e2e4b39d92bdf17419d116daa8e63afd9e01d11592f39df4da69d7'
            'SKIP')

_targets="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "$srcdir"
  for _target in ${_targets}; do
    echo "Building ${_target} CRT"
    if [ ${_target} == "i686-w64-mingw32" ]; then
        _crt_configure_args="--disable-lib64 --enable-lib32"
    elif [ ${_target} == "x86_64-w64-mingw32" ]; then
        _crt_configure_args="--disable-lib32 --enable-lib64"
    elif [ ${_target} == "aarch64-w64-mingw32" ]; then
        _crt_configure_args="--disable-lib32 --disable-lib64 --disable-libarm32 --enable-libarm64"
    fi
    if [[ $pkgname =~ .*-clang-.* ]]; then
        _crt_configure_args+=" --enable-cfguard"
    fi
    if [[ $pkgname =~ .*-clang-.* ]] || [[ $pkgname =~ .*-ucrt-.* ]]; then
      _crt_configure_args+=" --with-default-msvcrt=ucrt --disable-dependency-tracking"
    fi
    echo "extra args: $_crt_configure_args"
    mkdir -p "$srcdir"/crt-${_target} && cd "$srcdir"/crt-${_target}
    "$srcdir"/mingw-w64-v${pkgver}/mingw-w64-crt/configure --prefix=/usr/${_target} \
        --host=${_target} --enable-wildcard \
        ${_crt_configure_args}
    make
  done
}

package() {
  for _target in ${_targets}; do
    echo "Installing ${_target} crt"
    cd "$srcdir"/crt-${_target}
    make DESTDIR="$pkgdir" install

    if [[ $pkgname =~ .*-clang-.* ]]; then
      # Create empty dummy archives, to avoid failing when the compiler driver
      # adds -lssp -lssh_nonshared when linking.
      ar rcs "${pkgdir}"/usr/${_target}/lib/libssp.a
      ar rcs "${pkgdir}"/usr/${_target}/lib/libssp_nonshared.a
    fi
  done
  # Licenses
  install -Dm644 "$srcdir"/mingw-w64-v${pkgver}/COPYING.MinGW-w64/COPYING.MinGW-w64.txt "$pkgdir"/usr/share/licenses/${pkgname}/COPYING.MinGW-w64.txt
  install -Dm644 "$srcdir"/mingw-w64-v${pkgver}/COPYING.MinGW-w64-runtime/COPYING.MinGW-w64-runtime.txt "$pkgdir"/usr/share/licenses/${pkgname}/COPYING.MinGW-w64-runtime.txt
  install -Dm644 "$srcdir"/mingw-w64-v${pkgver}/mingw-w64-headers/ddk/readme.txt "$pkgdir"/usr/share/licenses/${pkgname}/ddk-readme.txt

}
