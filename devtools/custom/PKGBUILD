# Maintainer of regular devtools package: Pierre Schmitz <pierre@archlinux.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patches to fix issues with how we use the tools

# "custom": Martchus <martchus@gmx.net>
#  - patches to fix issues with how I use the tools

pkgname=devtools-custom
_pkgname=devtools
epoch=1
pkgver=1.2.1
pkgrel=1
pkgdesc='Tools for Arch Linux package maintainers (patches from ALARM and custom ones)'
arch=('any')
license=('GPL')
url='https://martchus.dyn.f3l.de/gitea/Martchus/devtools/src/branch/custom'
conflicts=('devtools' 'devtools-alarm')
provides=('devtools' 'devtools-alarm')
depends=(
  arch-install-scripts
  awk
  bash
  binutils
  coreutils
  curl
  diffutils
  expac
  fakeroot
  findutils
  grep
  jq
  openssh
  parallel
  rsync
  sed
  util-linux

  breezy
  git
  mercurial
  subversion
)
makedepends=(
  asciidoctor
  shellcheck
)
checkdepends=(
  bats
)
optdepends=(
  'btrfs-progs: btrfs support'
  'bat: pretty printing for pkgctl search'
  'nvchecker: pkgctl version subcommand'
)
source=("git+https://martchus.dyn.f3l.de/gitea/Martchus/$_pkgname.git#branch=custom-v$pkgver")
sha256sums=('SKIP')

build() {
  cd "$srcdir/${_pkgname}"
  make BUILDTOOLVER="${epoch}:${pkgver}-${pkgrel}-${arch}" PREFIX=/usr
}

check() {
  cd "$srcdir/${_pkgname}"
  make PREFIX=/usr test
}

package() {
  cd "$srcdir/${_pkgname}"
  make PREFIX=/usr DESTDIR="${pkgdir}" install
}
