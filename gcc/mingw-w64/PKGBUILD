# Maintainer of official package: Felix Yan <felixonmars@archlinux.org>
# Maintainer of official package: Peter Jung <ptr1337@archlinux.org>

pkgname=mingw-w64-gcc
pkgver=15.1.1+r7+gf36ec88aa85a
pkgrel=1
pkgdesc="Cross GCC for the MinGW-w64 cross-compiler"
arch=('x86_64')
url="https://gcc.gnu.org"
license=(GPL-3.0-with-GCC-exception GFDL-1.3-or-later)
groups=('mingw-w64-toolchain' 'mingw-w64')
depends=(
  bash
  gcc-libs
  glibc
  gmp
  libisl
  libmpc
  mingw-w64-binutils
  mingw-w64-crt
  mingw-w64-headers
  mingw-w64-winpthreads
  mpfr
  python
  zlib
  zstd
)
# gcc-d doesn't build as of 13.1.0
makedepends=(gcc-ada git)
options=('!strip' 'staticlibs' '!emptydirs' '!buildflags')
_commit=f36ec88aa85a1a8f4ec300dfcd862fc4fbca1c53
# using GitHub mirror as sourceware.org is very slow
_repo=https://github.com/gcc-mirror/gcc.git
#_repo=https://sourceware.org/git/gcc.git
source=(git+${_repo}#commit=${_commit})
#source=(https://ftp.gnu.org/gnu/gcc/gcc-$pkgver/gcc-$pkgver.tar.xz{,.sig})
validpgpkeys=(F3691687D867B81B51CE07D9BBE43771487328A9  # bpiotrowski@archlinux.org
              86CFFCA918CF3AF47147588051E8B148A9999C34  # evangelos@foutrelis.com
              13975A70E63C361C73AE69EF6EEB81F8981C74C7  # richard.guenther@gmail.com
              D3A93CAD751C2AF4F8C7AD516C35B99309B5FA62) # Jakub Jelinek <jakub@redhat.com>
#sha512sums=('ddd35ca6c653dffa88f7c7ef9ee4cd806e156e0f3b30f4d63e75a8363361285cd566ee73127734cde6a934611de815bee3e32e24bfd2e0ab9f7ff35c929821c1'
#            'SKIP')
sha512sums=('SKIP')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  #ln -sf gcc-${pkgver/+/-} gcc
  cd gcc

  # mmapio.c:69:14: error: implicit declaration of function ‘getpagesize’
  sed -i 's|\-Werror||g' libbacktrace/configure
}

build() {
  # Avoid errors due to implicit function declarations and similar issues (which are treated as errors as of GCC 14,
  # see https://gcc.gnu.org/gcc-14/porting_to.html)
  export CFLAGS_FOR_TARGET=-fpermissive

  # removing --enable-languages=ada as the build is broken as of GCC 15.1.0

  for _arch in ${_architectures}; do
    mkdir -p "$srcdir"/build-${_arch} && cd "$srcdir"/build-${_arch}
    "$srcdir"/gcc/configure --prefix=/usr --libexecdir=/usr/lib \
        --target=${_arch} \
        --with-bugurl=https://bugs.archlinux.org/ \
        --enable-languages=c,c++,fortran,lto,objc,obj-c++ \
        --enable-shared --enable-static \
        --enable-threads=posix --enable-fully-dynamic-string \
        --enable-libstdcxx-time=yes --enable-libstdcxx-filesystem-ts=yes \
        --with-system-zlib --enable-cloog-backend=isl \
        --enable-lto --enable-libgomp \
        --disable-multilib --enable-checking=release \
        --disable-sjlj-exceptions --with-dwarf2
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir"/build-${_arch}
    make DESTDIR="$pkgdir" install
    ${_arch}-strip "$pkgdir"/usr/${_arch}/lib/*.dll
    strip "$pkgdir"/usr/bin/${_arch}-*
    # removing gnat1 from strip list as ada is not enabled
    strip "$pkgdir"/usr/lib/gcc/${_arch}/*/{cc1*,collect2,f951,lto*}
    ln -s ${_arch}-gcc "$pkgdir"/usr/bin/${_arch}-cc
    # mv dlls
    mkdir -p "$pkgdir"/usr/${_arch}/bin/
    mv "$pkgdir"/usr/${_arch}/lib/*.dll "$pkgdir"/usr/${_arch}/bin/
  done
  strip "$pkgdir"/usr/bin/*
  # remove unnecessary files
  rm -r "$pkgdir"/usr/share
  rm "$pkgdir"/usr/lib/libcc1.*
}
