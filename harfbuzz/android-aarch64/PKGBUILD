# Maintainer of AUR version: Gonzalo Exequiel Pedone <hipersayan DOT x AT gmail DOT com>
# Contributor: Martchus < martchus at gmx DOT net >
# Contributor: pingplug < aur at pingplug dot me >
# Contributor: Schala Zeal < schalaalexiazeal at gmail dot com >

_android_arch=aarch64

pkgbase=android-${_android_arch}-harfbuzz
pkgname=("android-${_android_arch}-harfbuzz"
         "android-${_android_arch}-harfbuzz-icu")
pkgver=12.3.0
pkgrel=1
pkgdesc="OpenType text shaping engine (Android ${_android_arch})"
arch=('any')
license=('MIT')
url="https://www.freedesktop.org/wiki/Software/HarfBuzz"
groups=('android-harfbuzz')
depends=("android-${_android_arch}-glib2"
         "android-${_android_arch}-graphite"
         "android-${_android_arch}-freetype2")
makedepends=('android-meson'
             "android-${_android_arch}-cairo"
             "android-${_android_arch}-icu"
             'python'
             'ragel'
             'git')
options=(!strip !buildflags staticlibs !emptydirs)
source=("git+https://github.com/harfbuzz/harfbuzz?signed#tag=$pkgver")
b2sums=('6f415ac0aa2ce7f2cd0bfe960ce7c331283671e1c14bd0f8aa204d403c8a27638295dd58ae1891b13e7a27dc4abb19847381db4dbf7445de2db497860c52f61d')
validpgpkeys=(
  053D20F17CCCA9651B2C6FCB9AB24930C0B997A2 # Khaled Hosny <khaled@aliftype.com> (@khaledhosny)
  9F377DDB6D3153A48EB3EB1E63CC496475267693 # Caleb Maclennan <caleb@alerque.com> (@alerque)
  2277650A4E8BDFE4B7F6BE419FEE04E5D3531115 # Ebrahim Byagowi <ebrahim@gnu.org> (@ebraminio)
  EACF64F53455E2771BA661A4803B21859F015E4E # Behdad Esfahbod <behdad@behdad.org> (@behdad)
)

build() {
    cd harfbuzz
    source android-env ${_android_arch}

    android-${_android_arch}-meson build-shared \
        -D b_lto=false \
        -D graphite=enabled \
        -D tests=disabled \
        -D docs=disabled
    sed -i 's|-Wl,--no-undefined||g' build-shared/build.ninja
    ninja -C build-shared

    android-${_android_arch}-meson build-static \
        --default-library static \
        -D b_lto=false \
        -D graphite=enabled \
        -D tests=disabled \
        -D docs=disabled
    sed -i 's|-Wl,--no-undefined||g' build-static/build.ninja
    ninja -C build-static
}

package_android-aarch64-harfbuzz() {
    cd harfbuzz
    source android-env ${_android_arch}

    DESTDIR="${pkgdir}" ninja -C build-shared install
    DESTDIR="${pkgdir}" ninja -C build-static install

    cp "src/hb-ft.h" "${pkgdir}/${ANDROID_PREFIX_INCLUDE}/harfbuzz/"
    rm -r "${pkgdir}/${ANDROID_PREFIX_BIN}"
    ${ANDROID_STRIP} -g "${pkgdir}/${ANDROID_PREFIX_LIB}"/*.a || true
    ${ANDROID_STRIP} -g --strip-unneeded "${pkgdir}/${ANDROID_PREFIX_LIB}"/*.so

    mkdir -p "hb-icu/${ANDROID_PREFIX}"/{include/harfbuzz,lib/pkgconfig}
    mv -vf "${pkgdir}/${ANDROID_PREFIX_LIB}"/libharfbuzz-icu* "hb-icu/${ANDROID_PREFIX_LIB}"
    mv -vf "${pkgdir}/${ANDROID_PREFIX_LIB}/pkgconfig/harfbuzz-icu.pc" "hb-icu/${ANDROID_PREFIX_LIB}/pkgconfig"
    mv -vf "${pkgdir}/${ANDROID_PREFIX_INCLUDE}/harfbuzz/hb-icu.h" "hb-icu/${ANDROID_PREFIX_INCLUDE}/harfbuzz"
}

package_android-aarch64-harfbuzz-icu() {
    pkgdesc="OpenType text shaping engine (ICU integration, Android ${_android_arch})"
    depends=("android-${_android_arch}-harfbuzz"
             "android-${_android_arch}-icu")

    mkdir -p "${pkgdir}/${ANDROID_PREFIX}"
    mv -vf "${srcdir}/harfbuzz/hb-icu/${ANDROID_PREFIX}"/* "${pkgdir}/${ANDROID_PREFIX}"
 }
