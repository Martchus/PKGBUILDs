pkgname=mingw-w64-clang-aarch64-headers
pkgver=12.0.0
pkgrel=1
pkgdesc="MinGW-w64 headers for Windows"
arch=('any')
url="https://www.mingw-w64.org"
license=('custom')
groups=('mingw-w64-toolchain' 'mingw-w64')
options=('!strip' '!libtool' '!emptydirs')
validpgpkeys=('CAF5641F74F7DFBA88AE205693BDB53CD4EBC740')
source=(https://sourceforge.net/projects/mingw-w64/files/mingw-w64/mingw-w64-release/mingw-w64-v${pkgver}.tar.bz2{,.sig})
sha512sums=('949b2bfab8763ab10ec4e9fdfdaf5361517a4ab787fb98ab419b38d02694061c2e821ebbf6e2e4b39d92bdf17419d116daa8e63afd9e01d11592f39df4da69d7'
            'SKIP')

_targets="aarch64-w64-mingw32"

build() {
  for _target in ${_targets}; do
    echo "Configuring ${_target} headers"
    mkdir -p "$srcdir"/headers-${_target} && cd "$srcdir"/headers-${_target}
    if [[ $pkgname =~ .*-clang-.* ]]; then
        _configure_args+=" --enable-cfguard"

        export AR=llvm-ar ASM=clang LD=lld-link CC=clang CXX=clang++ RANLIB=llvm-ranlib DLLTOOL=llvm-dlltool
        export ARFLAGS=""
        export ASMFLAGS="-rtlib=compiler-rt -fuse-ld=lld -mguard=cf -target $_target -Xclang -triple -Xclang $_target"
        export LDFLAGS="-target $_target -Xclang -triple -Xclang $_target"
        export CFLAGS="-rtlib=compiler-rt -fuse-ld=lld -target $_target -Xclang -triple -Xclang $_target"
        export CXXFLAGS="-rtlib=compiler-rt -fuse-ld=lld -stdlib=libc++ -target $_target -Xclang -triple -Xclang $_target"
    fi
    if [[ $pkgname =~ .*-clang-.* ]] || [[ $pkgname =~ .*-ucrt-.* ]]; then
      _configure_args+=" --enable-idl --with-default-win32-winnt=0x601 --with-default-msvcrt=ucrt"
    fi
    "$srcdir"/mingw-w64-v${pkgver}/mingw-w64-headers/configure --prefix=/usr/${_target} --enable-sdk=all --host=${_target} ${_configure_args}
  done
}

package() {
  for _target in ${_targets}; do
    echo "Installing ${_target} headers"
    cd "$srcdir"/headers-${_target}
    make DESTDIR="$pkgdir" install
    if [[ $_target != aarch64-w64-mingw32 ]]; then
      rm "$pkgdir"/usr/${_target}/include/pthread_signal.h
      rm "$pkgdir"/usr/${_target}/include/pthread_time.h
      rm "$pkgdir"/usr/${_target}/include/pthread_unistd.h
    fi
  done

  echo "Installing MinGW-w64 licenses"
  install -Dm644 "$srcdir"/mingw-w64-v${pkgver}/COPYING.MinGW-w64/COPYING.MinGW-w64.txt "$pkgdir"/usr/share/licenses/${pkgname}/COPYING.MinGW-w64.txt
  install -Dm644 "$srcdir"/mingw-w64-v${pkgver}/COPYING.MinGW-w64-runtime/COPYING.MinGW-w64-runtime.txt "$pkgdir"/usr/share/licenses/${pkgname}/COPYING.MinGW-w64-runtime.txt
  install -Dm644 "$srcdir"/mingw-w64-v${pkgver}/mingw-w64-headers/ddk/readme.txt "$pkgdir"/usr/share/licenses/${pkgname}/ddk-readme.txt
}
