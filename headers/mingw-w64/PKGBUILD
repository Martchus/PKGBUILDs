# Maintainer of official package: Felix Yan <felixonmars@archlinux.org>

pkgname=mingw-w64-headers
_name=mingw-w64
pkgver=13.0.0
pkgrel=3
pkgdesc="MinGW-w64 headers for Windows"
arch=('any')
url="https://www.mingw-w64.org"
license=('LicenseRef-custom')
groups=('mingw-w64-toolchain' 'mingw-w64')
[[ $pkgname =~ .*-clang-.* ]] && makedepends=('mingw-w64-configure')
options=('!strip' '!libtool' '!emptydirs')
validpgpkeys=('CAF5641F74F7DFBA88AE205693BDB53CD4EBC740')
source=(https://sourceforge.net/projects/$_name/files/$_name/$_name-release/$_name-v${pkgver}.tar.bz2{,.sig}
        0002-mswsock-add-SIO_UDP_NETRESET.patch)
sha512sums=('39a22bd92465f571df1d90ac80b88dc6846998f71e4b7db79d1c9aa6e04cb3e41b632990a7238fc2c384ea67e4a77d103230db2c0d741869340d24ea79b6dda8'
            'SKIP'
            'de3fa3284e4a9ba07723dbcc44bc9f86980550061b9dde3d716743166696240893c0550191b07f4e1377eed2a870f7df8f95e04cf274e79086bbc4b834b641cc')

_targets="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "$srcdir"/mingw-w64-v${pkgver}

  # add SIO_UDP_NETRESET required by OpenSSL since https://github.com/openssl/openssl/commit/4a7d9705f30842b402058324a6947938fe3486ec
  patch -p1 -i ../0002-mswsock-add-SIO_UDP_NETRESET.patch
}

build() {
  for _target in ${_targets}; do
    echo "Configuring ${_target} headers"
    mkdir -p "$srcdir"/headers-${_target} && cd "$srcdir"/headers-${_target}
    if [[ $pkgname =~ .*-clang-.* ]]; then
        _configure_args+=" --enable-cfguard"
    fi
    if [[ $pkgname =~ .*-clang-.* ]] || [[ $pkgname =~ .*-ucrt-.* ]]; then
      _configure_args+=" --enable-idl --with-default-win32-winnt=0x601 --with-default-msvcrt=ucrt"
    fi
    "$srcdir"/mingw-w64-v${pkgver}/mingw-w64-headers/configure --prefix=/usr/${_target} --enable-sdk=all --host=${_target} ${_configure_args}
  done
}

package() {
  for _target in ${_targets}; do
    echo "Installing ${_target} headers"
    cd "$srcdir"/headers-${_target}
    make DESTDIR="$pkgdir" install
    rm "$pkgdir"/usr/${_target}/include/pthread_signal.h
    rm "$pkgdir"/usr/${_target}/include/pthread_time.h
    rm "$pkgdir"/usr/${_target}/include/pthread_unistd.h
  done

  echo "Installing MinGW-w64 licenses"
  install -Dm644 "$srcdir"/mingw-w64-v${pkgver}/COPYING.MinGW-w64/COPYING.MinGW-w64.txt "$pkgdir"/usr/share/licenses/${pkgname}/COPYING.MinGW-w64.txt
  install -Dm644 "$srcdir"/mingw-w64-v${pkgver}/COPYING.MinGW-w64-runtime/COPYING.MinGW-w64-runtime.txt "$pkgdir"/usr/share/licenses/${pkgname}/COPYING.MinGW-w64-runtime.txt
  install -Dm644 "$srcdir"/mingw-w64-v${pkgver}/mingw-w64-headers/ddk/readme.txt "$pkgdir"/usr/share/licenses/${pkgname}/ddk-readme.txt
}
