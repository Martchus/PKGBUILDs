# Maintainer of AUR version: pingplug < aur at pingplug dot me >
# Contributor: Schala Zeal < schalaalexiazeal at gmail dot com >
# Contributor: ant32 < antreimer at gmail dot com >
# Contributor: Alexey Pavlov < Alexpux at gmail dot com >
# Contributor: Ray Donnelly < mingw dot android at gmail dot com >

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

pkgname=mingw-w64-icu
pkgver=78.2
pkgrel=1
pkgdesc="International Components for Unicode library (mingw-w64)"
arch=('any')
url="https://icu.unicode.org/"
license=('LicenseRef-Unicode-3.0'
         'BSD-2-Clause'
         'BSD-3-Clause'
         'NAIST-2003')
depends=('mingw-w64-crt')
makedepends=('mingw-w64-configure' 'autoconf-archive')
options=('!strip' 'staticlibs' '!buildflags')
source=("https://github.com/unicode-org/icu/releases/download/release-${pkgver}/icu4c-${pkgver}-sources.tgz"{,.asc}
        "0015-debug.mingw.patch"
        "0016-icu-pkgconfig.patch"
        "0021-mingw-static-libraries-without-s.patch")
sha512sums=('92feddfe81c57336f386c7cbc9f6d976bf349db148a77a247c4559676f51116115c8c52c4d907feb50933f72ab75fd8e48be092bf9c8ca33a3e8fabc9372a5d6'
            'SKIP'
            '72c2bfa46e0ddc2d373860b9e96cd01e3643fff2d1dc105296b5d1dab57c7fa129affd197d8b85f7501904629a6472fbf3786fd56736350c06cb70e717d215e6'
            'fbbf2a78d9c8cc93eb039513ca049deb4c794c39a6e889a16d99990bf3dfcf52f5d77d5bf8a7ced72c076d40eeb37e9cc96ecf62c4d59f0e8da7782fa78335a8'
            'bd0ccb7d9e8c2c783eb02fe17f98e41e0c4970443c94acd89cec1dec2eb939481b8096fcbfbb193d632c9f757003afb65d3be417f10cee059afa2e274fb66472')
validpgpkeys=("FFA9129A180D765B7A5BEA1C9B432B27D1BA20D7"
              "3DA35301A7C330257B8755754058F67406EAA6AB"
              "0E51E7F06EF719FBD072782A5F56E5AFA63CCD33"
              "4569BBC09DA846FC91CBD21CE1BBA44593CF2AE0"
              "E52F07877A5805F9AF4AB0ACD46C5610D06E7001")

prepare() {
  cd icu
  patch -p1 -i ../0015-debug.mingw.patch
  patch -p1 -i ../0016-icu-pkgconfig.patch
  patch -p1 -i ../0021-mingw-static-libraries-without-s.patch

  cd source
  autoreconf -fi
}

build() {
  cd icu/source
  mkdir -p nativebuild && pushd nativebuild
  CC=gcc CXX=g++ ../configure --enable-static --disable-shared
  make
  popd
  # add flag to prevent `error: unable to find numeric literal operator ‘operator""Q’`
  [[ $pkgname =~ .*-clang-.* ]] || export CFLAGS+=-fext-numeric-literals CXXFLAGS+=-fext-numeric-literals
  for _arch in ${_architectures}; do
    mkdir build-${_arch} && pushd build-${_arch}
    ${_arch}-configure \
      --with-cross-build=${PWD}/../nativebuild \
      --with-data-packaging=library \
      --disable-rpath \
      --enable-release \
      --disable-tests \
      --disable-samples
    make
    popd
  done
}

package() {
  mkdir -p "${pkgdir}/usr/bin"
  for _arch in ${_architectures}; do
    cd "${srcdir}/icu/source/build-${_arch}"
    make install DESTDIR="${pkgdir}"
    find "${pkgdir}/usr/${_arch}" -name '*.exe' -exec ${_arch}-strip {} \;
    find "${pkgdir}/usr/${_arch}" -name '*.dll' -exec ${_arch}-strip --strip-unneeded {} \;
    find "${pkgdir}/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs ${_arch}-strip -g
    ln -s "/usr/${_arch}/bin/icu-config" "${pkgdir}/usr/bin/${_arch}-icu-config"
  done
}

# vim:set ts=2 sw=2 et:
