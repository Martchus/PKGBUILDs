# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

_pkgname=libgpg-error
pkgname=static-compat-$_pkgname
pkgver=1.58
pkgrel=1
pkgdesc="Support library for libgcrypt"
arch=(x86_64)
url="https://www.gnupg.org"
license=(LGPL-2.1-or-later
         'BSD-3-Clause OR LGPL-2.1-or-later'
         FSFULLR
         GPL-2.0-or-later)
depends=('glibc-static-compat' 'sh')
makedepends=('static-compat-configure')
options=(staticlibs)
source=(https://www.gnupg.org/ftp/gcrypt/libgpg-error/${_pkgname}-${pkgver}.tar.bz2{,.sig})
# https://www.gnupg.org/download/integrity_check.html
sha256sums=('f943aea9a830a8bd938e5124b579efaece24a3225ff4c3d27611a80ce1260c27'
            'SKIP')
validpgpkeys=('6DAA6E64A76D2840571B4902528897B826403ADA') # Werner Koch (dist signing 2020)
validpgpkeys+=('AC8E115BF73E2D8D47FA9908E98E9B2D19C6C8BD') # Niibe Yutaka (GnuPG Release Key)

prepare() {
  source static-compat-environment
  cd ${_pkgname}-${pkgver}
  autoreconf -vfi
}

build() {
  source static-compat-environment
  cd ${_pkgname}-${pkgver}
  static-compat-configure --disable-shared --enable-static
  make
}

check() {
  source static-compat-environment
  cd ${_pkgname}-${pkgver}
  #make check
  # note: fails with error message "FAIL: gpg-error-config-test.sh"
}

package() {
  source static-compat-environment
  cd ${_pkgname}-${pkgver}
  make DESTDIR="${pkgdir}/" install
  rm -fr "$pkgdir/$static_compat_prefix"/share/{man,info}
  install -vDm 644 COPYING{,.LIB} -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
