pkgname=mingw-w64-clang-aarch64-libiconv
pkgver=1.17
pkgrel=1
arch=(any)
pkgdesc="Provides GNU libiconv.so and libcharset.so (mingw-w64-clang-aarch64)"
depends=(mingw-w64-clang-aarch64-crt)
makedepends=(mingw-w64-clang-aarch64-configure)
options=(!strip !buildflags staticlibs)
license=("LGPL")
url="http://www.gnu.org/software/libiconv/"
source=("http://ftp.gnu.org/pub/gnu/libiconv/libiconv-$pkgver.tar.gz")
sha256sums=('8f74213b56238c85a50a5329f77e06198771e70dd9a739779f4c02f65d971313')

_architectures="aarch64-w64-mingw32"


build() {
  export USE_COMPILER_WRAPPERS=1
  cd "$srcdir/libiconv-$pkgver"
  for _arch in ${_architectures}; do
    source mingw-clang-env $_arch
    mkdir -p build-${_arch} && pushd build-${_arch}
    ${_arch}-configure --disable-nls
    make
    popd
  done
}

package() {
  export USE_COMPILER_WRAPPERS=1
  for _arch in ${_architectures}; do
    source mingw-clang-env $_arch
    cd "${srcdir}/libiconv-${pkgver}/build-${_arch}"
    make DESTDIR="$pkgdir" install -j1
    rm -r "$pkgdir"/usr/${_arch}/share/
    rm "$pkgdir"/usr/${_arch}/bin/*.exe
    $STRIP --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    $STRIP -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}

