# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Simone Sclavi 'Ito' <darkhado@gmail.com>

_pkgname=libjpeg-turbo
pkgname=static-compat-$_pkgname
pkgver=3.1.0
pkgrel=1
pkgdesc="JPEG image codec with accelerated baseline compression and decompression"
arch=(x86_64)
url="https://libjpeg-turbo.org/"
license=(
  BSD-3-Clause
  IJG
)
depends=(glibc-static-compat)
makedepends=(static-compat-cmake nasm ninja)
options=(staticlibs)
source=("https://github.com/libjpeg-turbo/libjpeg-turbo/releases/download/$pkgver/libjpeg-turbo-$pkgver.tar.gz")
validpgpkeys=('0338C8D8D9FDA62CF9C421BD7EC2DBB6F4DBF434') # The libjpeg-turbo Project (Signing key for official binaries) <information@libjpeg-turbo.org>
sha512sums=('63231854529f59a95814cf2414fe384360a4ed96c572764fabc535c3770c559d26943e129c905a63d5949088fa79138b1ffdb175b817f2f4d36329a3cbb23ea7')

build() {
  source static-compat-environment
  local cmake_options=(
    -B build
    -DCMAKE_INSTALL_LIBDIR="$static_compat_prefix"/lib
    -DWITH_JAVA=OFF
    -DWITH_JPEG8=ON
    -G Ninja
    -S $_pkgname-$pkgver
    -W no-dev
  )
  static-compat-cmake "${cmake_options[@]}"
  cmake --build build -v
}

check() {
  source static-compat-environment
  ctest --test-dir build --output-on-failure --stop-on-failure -j$(nproc)
}

package() {
  source static-compat-environment
  export docdir="$static_compat_prefix"/share/doc/$_pkgname exampledir="$static_compat_prefix"/share/doc/$_pkgname
  DESTDIR="$pkgdir" cmake --install build -v

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 $_pkgname-$pkgver/LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $_pkgname-$pkgver/README.ijg -t "$pkgdir/usr/share/licenses/$pkgname/"
  rm -r "$pkgdir/$static_compat_prefix"/share
  find "$pkgdir/$static_compat_prefix"/lib -iname '*.so*' -delete
}
