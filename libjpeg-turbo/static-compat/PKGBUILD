# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Simone Sclavi 'Ito' <darkhado@gmail.com>

_pkgname=libjpeg-turbo
pkgname=static-compat-$_pkgname
pkgver=3.1.2
pkgrel=1
pkgdesc="JPEG image codec with accelerated baseline compression and decompression"
arch=(x86_64)
url="https://libjpeg-turbo.org/"
_url="https://github.com/libjpeg-turbo/libjpeg-turbo/"
license=(
  BSD-3-Clause
  IJG
)
depends=(glibc-static-compat)
makedepends=(static-compat-cmake nasm ninja)
options=(staticlibs)
source=(
  $_url/releases/download/$pkgver/$_pkgname-$pkgver.tar.gz{,.sig}
)
validpgpkeys=('0338C8D8D9FDA62CF9C421BD7EC2DBB6F4DBF434') # The libjpeg-turbo Project (Signing key for official binaries) <information@libjpeg-turbo.org>
sha512sums=('79271ae4ddc12e3753cc7323dc15617f1d82b2d554ef27b555712f6ab5de603323dd33747620815e3b55663a20e07b292a55172aee9f401f9fd3557145967abe')
b2sums=('476280f1140977903e177667184452b8ec79a930cd0dbcfc10f46c682c85e2daf38ff8a4267590859eb845a9273fc91b19a61168b7e2c5229d7ed4c18100b2dd'
         'SKIP')

build() {
  source static-compat-environment
  local cmake_options=(
    -B build
    -DCMAKE_INSTALL_LIBDIR="$static_compat_prefix"/lib
    -DWITH_JAVA=OFF
    -DWITH_JPEG8=ON
    -G Ninja
    -S $_pkgname-$pkgver
    -W no-dev
  )
  static-compat-cmake "${cmake_options[@]}"
  cmake --build build -v
}

check() {
  source static-compat-environment
  ctest --test-dir build --output-on-failure --stop-on-failure -j$(nproc)
}

package() {
  source static-compat-environment
  export docdir="$static_compat_prefix"/share/doc/$_pkgname exampledir="$static_compat_prefix"/share/doc/$_pkgname
  DESTDIR="$pkgdir" cmake --install build -v

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 $_pkgname-$pkgver/LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $_pkgname-$pkgver/README.ijg -t "$pkgdir/usr/share/licenses/$pkgname/"
  rm -r "$pkgdir/$static_compat_prefix"/share
  find "$pkgdir/$static_compat_prefix"/lib -iname '*.so*' -delete
}
