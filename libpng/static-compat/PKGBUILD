# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Travis Willard <travis@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

_pkgname=libpng
pkgname=static-compat-$_pkgname
pkgver=1.6.50
pkgrel=1
pkgdesc='A collection of routines used to create PNG format graphics files'
arch=('x86_64')
url='http://www.libpng.org/pub/png/libpng.html'
license=('libpng-2.0')
depends=('static-compat-zlib' 'sh')
makedepends=('git' 'static-compat-configure')
options=(!emptydirs staticlibs)
source=("git+https://github.com/pnggroup/libpng.git?signed#tag=v${pkgver}")
sha256sums=('7def7cd90b18267c7d4242288e6aafc97077ff59b5454ab9a99ee8b6e9171047')
validpgpkeys=('F57A55036A4D45837074FD92C9E384533403C2F8'  # Cosmin Truta <ctruta@gmail.com>
              '1FED507E3236B4A6E53E922BB292C64843FF5BCF') # Cosmin Truta <ctruta@gmail.com>

build() {
  source static-compat-environment
  cd $_pkgname
  static-compat-configure \
    --disable-shared \
    --enable-static
  make
}

check() {
  source static-compat-environment
  cd $_pkgname

  make check
}

package() {
  source static-compat-environment
  cd $_pkgname

  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir/$static_compat_prefix"/share/man

  install -D -m0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
