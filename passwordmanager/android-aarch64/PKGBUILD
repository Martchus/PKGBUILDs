# Maintainer: Martchus <martchus@gmx.net>

# All my PKGBUILDs are managed at https://github.com/Martchus/PKGBUILDs where
# you also find the URL of a binary repository.

# FIXME: Set versions of the various repositories.

_arch=aarch64
_pkgver_cpputilities=
_pkgver_qtutilities=
_pkgver_passwordfile=
_reponame=passwordmanager
pkgname=android-$_arch-passwordmanager
pkgver=4.2.4
pkgrel=1
arch=('any')
pkgdesc="Tray application for Syncthing (Android, $_arch)"
license=(GPL-2.0-or-later)
depends=("android-$_arch-qt6-svg" "android-$_arch-qt6-declarative" "android-$_arch-openssl"
         "android-$_arch-libiconv" "android-$_arch-boost" "android-$_arch-kirigami")
makedepends=('android-cmake' 'android-ndk' 'ninja' 'git' 'clang' 'qt6-tools' 'qt6-declarative'
             "android-$_arch-qt6-tools" 'breeze-icons')
url="https://github.com/Martchus/${_reponame}"
_github_url=git+https://github.com/Martchus
_git_url=${MARTCHUS_GIT_URL_PREFIX:-$_github_url}
source=(
  "c++utilities::$_git_url/cpp-utilities" #tag=v$_pkgver_cpputilities"
  "qtutilities::$_git_url/qtutilities" #tag=v$_pkgver_qtutilities"
  "passwordfile::$_git_url/passwordfile" #tag=v$_pkgver_passwordfile"
  "passwordmanager::$_git_url/${_reponame}" #tag=v$pkgver"
  "subdirs::$_git_url/subdirs"
  "keystore::keystore"
)
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            '912f86c4582aeecc2ebd5366acc0ea45bc6e293c9fefb5f2cca15a96aef5cb11')
options=(!buildflags staticlibs !strip !emptydirs)

prepare() {
  cd $_reponame
}

build() {
  source android-env $_arch
  export BUILD_DIR=$srcdir/build
  export QT_ANDROID_KEYSTORE_PATH=$srcdir/keystore

  cd subdirs/$_reponame
  cmake --preset arch-android \
    -DBUILTIN_ICON_THEMES:STRING='breeze;breeze-dark'
  cmake --build --preset arch-android
}

package() {
  source android-env $_arch
  export BUILD_DIR=$srcdir/build
  local apk_dir=$pkgdir/$ANDROID_PREFIX/apk
  DESTDIR=$pkgdir cmake --install "$BUILD_DIR/$_reponame"/arch-android-*
  mkdir -p "$apk_dir"
  find "$BUILD_DIR" -iname '*-signed.apk' -exec cp -v --target-directory="$apk_dir" {} \+
  find "$pkgdir" -name 'lib*.so' -type f -exec "$ANDROID_STRIP" --strip-unneeded {} \;
  find "$pkgdir" -name 'lib*.a' -type f -exec "$ANDROID_STRIP" -g {} \;
  mv -v "$apk_dir"/*.apk "$apk_dir/$_reponame.apk"
}
