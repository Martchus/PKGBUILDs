# Maintainer: Martchus <martchus@gmx.net>

# All my PKGBUILDs are managed at https://github.com/Martchus/PKGBUILDs where
# you also find the URL of a binary repository.

_reponame=passwordmanager
pkgname=mingw-w64-passwordmanager-qt6
_name=${pkgname#mingw-w64-}
_name=${_name%-qt6}
pkgver=4.2.5
pkgrel=2
arch=('any')
pkgdesc='A simple password store using AES-256-CBC encryption via OpenSSL (mingw-w64)'
license=(GPL-2.0-or-later)
depends=('mingw-w64-crt' 'mingw-w64-qt6-base' 'mingw-w64-qtutilities-qt6' 'mingw-w64-passwordfile' 'mingw-w64-openssl')
makedepends=('mingw-w64-gcc' 'mingw-w64-cmake' 'mingw-w64-qt6-tools' 'qt6-tools' 'qt6-declarative' 'clang' 'ffmpeg' 'ninja')
url="https://github.com/Martchus/${_reponame}"
source=("${_name}-${pkgver}.tar.gz::https://github.com/Martchus/${_reponame}/archive/v${pkgver}.tar.gz")
sha256sums=('0eee2ae39c817f092f457f71018137514dc91d066278ffb7a2eb3743aa597855')
options=(!buildflags staticlibs !strip !emptydirs)

_architectures=(${MINGW_W64_QT6_ARCHS:-x86_64-w64-mingw32})
_configurations=()
[[ $NO_SHARED_LIBS ]] || _configurations+=('shared')
[[ $NO_STATIC_LIBS ]] || _configurations+=('static') makedepends+=('mingw-w64-cmake-static' 'mingw-w64-qt6-base-static' 'mingw-w64-qt6-translations' 'mingw-w64-qt6-svg-static' 'breeze-icons')

build() {
  declare -A _config_flags=(
    [shared]='
        -DBUILD_SHARED_LIBS:BOOL=ON
    '
    [static]='
        -DBUILD_SHARED_LIBS:BOOL=OFF
        -DCMAKE_FIND_LIBRARY_SUFFIXES:STRING=.a;.lib
        -DSTATIC_LINKAGE:BOOL=ON
        -DSTATIC_LIBRARY_LINKAGE:BOOL=ON
        -DPASSWORD_MANAGER_CONFIGURATION_TARGET_SUFFIX:STRING=qt6-static
        -DBUILTIN_TRANSLATIONS:BOOL=ON
        -DBUILTIN_ICON_THEMES:STRING=breeze;breeze-dark
        -DIMAGE_FORMAT_SUPPORT:STRING=Gif;ICO;Jpeg
        -DSVG_SUPPORT:BOOL=ON
        -DSVG_ICON_SUPPORT:BOOL=ON
    '
  )
  declare -A _cfg_suffix=(
    [static]='-static'
  )
  for _arch in "${_architectures[@]}"; do
    for _cfg in "${_configurations[@]}"; do
      msg2 "${_arch}-${_cfg}"
      ${_arch}-cmake${_cfg_suffix[$_cfg]} \
        -G Ninja \
        -S "$srcdir/${PROJECT_DIR_NAME:-$_reponame-$pkgver}" \
        -B "build-${_arch}-${_cfg}" \
        -DCMAKE_BUILD_TYPE:STRING='Release' \
        -DCMAKE_INSTALL_PREFIX="/usr/${_arch}" \
        -DCONFIGURATION_NAME:STRING="${_cfg}-qt6" \
        -DCONFIGURATION_DISPLAY_NAME="" \
        -DCONFIGURATION_PACKAGE_SUFFIX:STRING="-${_cfg}" \
        -DCONFIGURATION_PACKAGE_SUFFIX_QTUTILITIES:STRING="-${_cfg}-qt6" \
        -DCONFIGURATION_TARGET_SUFFIX:STRING="qt6" \
        -DPASSWORD_MANAGER_CONFIGURATION_TARGET_SUFFIX:STRING="qt6${_cfg_suffix[$_cfg]}" \
        -DQT_PACKAGE_PREFIX:STRING=Qt6 \
        -DKF_PACKAGE_PREFIX:STRING=KF6 \
        -DENABLE_TARGETS_FOR_MINGW64_CROSS_PACKAGING:BOOL=ON \
        -DQUICK_GUI:BOOL=OFF \
        ${_config_flags[$_cfg]}
      VERBOSE=1 cmake --build "build-${_arch}-${_cfg}"
    done
  done
}

package() {
  install \
    -D --target-directory="$pkgdir/usr/share/licenses/$pkgname" \
    "$srcdir/${PROJECT_DIR_NAME:-$_reponame-$pkgver}/LICENSES-windows-distribution.md"

  for _arch in "${_architectures[@]}"; do
    for _cfg in "${_configurations[@]}"; do
      msg2 "${_arch}-${_cfg}"
      DESTDIR="${pkgdir}" cmake --build "build-${_arch}-${_cfg}" --target install-mingw-w64-strip
    done
  done
}
