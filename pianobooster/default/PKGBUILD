_pkgname=pianobooster
pkgname=pianobooster
pkgdesc='A MIDI file player that teaches you how to play the piano (with custom, experimental patches)'
pkgver=1392.86ce9b0
pkgrel=1
depends=('ftgl' 'hicolor-icon-theme' 'qt6-base' 'libfluidsynth.so' 'librtmidi.so' 'libqtutilities-git.so')
makedepends=('git' 'cmake' 'clang' 'qt6-tools' 'qt6-declarative' 'ninja')
optdepends=('qt6-translations')
arch=('x86_64')
url='https://github.com/Martchus/PianoBooster'
license=('GPL3')
source=("${_pkgname}::${MARTCHUS_GIT_URL_PREFIX:-git+https://github.com/Martchus}/PianoBooster.git#branch=custom")
sha256sums=(SKIP)

pkgver() {
  cd "${srcdir}/${_pkgname}"
  echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build () {
  cmake -G Ninja -B build -S "${srcdir}/${_pkgname}" \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DUSE_SYSTEM_FONT=ON \
        -DUSE_JACK=ON \
        -DWITH_INTERNAL_FLUIDSYNTH=ON \
        -DCONFIGURATION_PACKAGE_SUFFIX=-git \
        -DCONFIGURATION_PACKAGE_SUFFIX_QTUTILITIES=-git \
        -DBUILTIN_TRANSLATIONS:BOOL=ON \
        -DQT_PACKAGE_PREFIX=Qt6 \
        -DWITH_MAN=ON
  VERBOSE=1 cmake --build build
}

package () {
  DESTDIR="$pkgdir" cmake --install build
}
