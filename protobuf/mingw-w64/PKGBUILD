pkgname=mingw-w64-protobuf
_pkgname=protobuf
pkgver=30.2
pkgrel=1
pkgdesc="Protocol Buffers - Google's data interchange format (mingw-w64)"
arch=('any')
url='https://developers.google.com/protocol-buffers/'
license=('BSD-3-Clause')
depends=(mingw-w64-zlib mingw-w64-abseil-cpp)
makedepends=(mingw-w64-cmake-static protobuf)
options=(!strip !buildflags staticlibs)
source=("https://github.com/protocolbuffers/${_pkgname}/releases/download/v$pkgver/${_pkgname}-$pkgver.tar.gz"
        0001-fix-building-shared-libs-with-clang.patch
        0002-windres-invocation.patch
        0003-demote-error-about-buildtime-runtime-version-difference-to-warning.patch)
sha256sums=('fb06709acc393cc36f87c251bb28a5500a2e12936d4346099f2c6240f6c7a941'
            '04096f913c1fd5b8c6703e3f696ae7d068c1576ef14fadad2e372da4af188dd0'
            '174f714b842d5153c79c5fda1ae775ee002aea11d53cb5d5f51cb5c4b9e63d29'
            '2b680e1c642ccd5bb70b3dcf7217c27360bc0b74f63d8afb66dd668173d9b2dd')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd ${_pkgname}-${pkgver}
  patch -p1 -i ../0001-fix-building-shared-libs-with-clang.patch
  patch -p1 -i ../0002-windres-invocation.patch
  patch -p1 -i ../0003-demote-error-about-buildtime-runtime-version-difference-to-warning.patch
}

build() {
  cd ${_pkgname}-${pkgver}
  local flags=(-Dprotobuf_BUILD_TESTS=OFF -Dprotobuf_ABSL_PROVIDER=package)
  for _arch in ${_architectures}; do
    ${_arch}-cmake -B build-${_arch} "${flags[@]}" .
    cmake --build build-${_arch}
    ${_arch}-cmake-static -B build-static-${_arch} -DCMAKE_INSTALL_PREFIX:PATH=/usr/$_arch/static "${flags[@]}" .
    cmake --build build-static-${_arch}
  done
}

package() {
  cd ${_pkgname}-${pkgver}
  for _arch in ${_architectures}; do
    DESTDIR=$pkgdir cmake --install build-${_arch}
    DESTDIR=$pkgdir cmake --install build-static-${_arch}
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a "$pkgdir"/usr/${_arch}/static/lib/*.a
  done
}
