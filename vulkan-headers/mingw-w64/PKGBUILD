# Maintainer of AUR version: Patrick Northon <northon_patrick3@yahoo.ca>

_pkgname=vulkan-headers
pkgname=mingw-w64-${_pkgname}
_dirname=Vulkan-Headers
pkgver=1.4.313.0
pkgrel=1
pkgdesc='Vulkan header files (mingw-w64)'
arch=(any)
url="https://www.vulkan.org/"
license=("Apache-2.0 OR MIT")
makedepends=(mingw-w64-cmake ninja git)
groups=('mingw-w64-vulkan-devel')
options=(!buildflags staticlibs !strip)
source=(
  "git+https://github.com/KhronosGroup/Vulkan-Headers#tag=vulkan-sdk-$pkgver"
  0001-Remove-Werror.patch
)
b2sums=('5e1cb2ba4a68fb4224ce5badb9b7d2d5da28ae2f7b440b502cc8433c82537458fc12c398539606981180e2937afdfca7e5179d082ece14bb597c08b3b7358f81'
        '5d7201f11850cdf8242057034e90c60ad7518c14fdcb091dc63ca6be6b10567c229f96b3dcdb3ecf57856f7b72bceb4048e4a6e5e1faf6bb98262324f408dbb8')

_srcdir="${_dirname}"
_architectures='i686-w64-mingw32 x86_64-w64-mingw32'

prepare() {
  cd Vulkan-Headers

  # GCC 15 fix
  git apply -3 ../0001-Remove-Werror.patch
}

build() {
  for _arch in ${_architectures}; do
    ${_arch}-cmake -G Ninja -S "${_srcdir}" -B "build-${_arch}"
    cmake --build "build-${_arch}"
  done
}

package() {
  for _arch in ${_architectures}; do
    DESTDIR="${pkgdir}" cmake --install "build-${_arch}"
  done
  install -Dm644 Vulkan-Headers/LICENSES/MIT.txt \
    -t "$pkgdir/usr/share/licenses/$pkgname"
}
