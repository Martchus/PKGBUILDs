# Contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=static-compat-vulkan-headers
_pkgname=Vulkan-Headers
pkgver=1.4.303
pkgrel=1
epoch=2
pkgdesc="Vulkan header files"
arch=(any)
url="https://www.khronos.org/vulkan/"
license=('Apache-2.0')
makedepends=(static-compat-cmake git)
provides=("static-compat-vulkan-hpp=${pkgver}")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/KhronosGroup/Vulkan-Headers/archive/v${pkgver}.tar.gz")
sha256sums=('fdf0e2e05356b455137ff97f837c9689ba253ec7d20f87ad81575b9bdbe7fdd4')

build() {
  source static-compat-environment
  cd Vulkan-Headers*

  rm -rf build ; mkdir build ; cd build
  static-compat-cmake ..
  make
}

package() {
  source static-compat-environment
  cd Vulkan-Headers*/build
  
  make DESTDIR="${pkgdir}" install
}
