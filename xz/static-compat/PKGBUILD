# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Fran√ßois Charette <firmicus@gmx.net>

_pkgname=xz
pkgname=static-compat-$_pkgname
pkgver=5.8.2
pkgrel=1
pkgdesc='Library and command line tools for XZ and LZMA compressed files'
arch=('x86_64')
url='https://tukaani.org/xz/'
license=("GPL-2.0-or-later AND 0BSD AND LicenseRef-Public-Domain AND LGPL-2.1-or-later")
depends=('glibc-static-compat')
makedepends=('static-compat-configure' 'git' 'po4a')
options=(!emptydirs staticlibs)
validpgpkeys=('3690C240CE51B4670D30AD1C38EE757D69184620')  # Lasse Collin <lasse.collin@tukaani.org>
source=("git+https://github.com/tukaani-project/xz#tag=v${pkgver}")
sha256sums=('c340ab4c34626b505d86824f1b2365a0314791c8edcacb201b67d112c54b94c0')
sha512sums=('328fe243e5701ac8216d21168a9e17ea9475cc08b27659c18b68230d8ddd0fffa9f831c576b08c160774fe2c4795604f82b64113e7f84db417b4bd26a4dae2c1')

prepare() {
  cd ${_pkgname}
  ./autogen.sh
}

build() {
  source static-compat-environment
  cd ${_pkgname}

  static-compat-configure --disable-shared --enable-static --disable-rpath --disable-doxygen
  make
}

check() {
  source static-compat-environment
  cd ${_pkgname}
  make check
}

package() {
  source static-compat-environment
  cd ${_pkgname}
  make DESTDIR="${pkgdir}" install
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}/"
  ln -sf /usr/static/share/doc/xz/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
  rm -r "$pkgdir/$static_compat_prefix"/bin
  rm -r "$pkgdir/$static_compat_prefix"/share/{man,doc,locale}
}
