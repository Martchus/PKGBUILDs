pkgname=mingw-w64-clang-aarch64-zlib
pkgver=1.3.1
pkgrel=2
pkgdesc="Compression library implementing the deflate compression method found in gzip and PKZIP (mingw-w64-clang-aarch64)"
arch=('any')
license=('Zlib')
depends=(mingw-w64-clang-aarch64-crt mingw-w64-clang-aarch64-cmake)
makedepends=(ninja)
url="http://www.zlib.net/"
source=("http://zlib.net/fossils/zlib-${pkgver}.tar.gz")
options=(!strip !buildflags staticlibs)
sha256sums=('9a93b2b7dfdac77ceba5a558a580e74667dd6fede4585b91eefb60f03b72df23')

_architectures="aarch64-w64-mingw32"

build() {
  for _arch in ${_architectures}; do
    source mingw-clang-env $_arch
    $_arch-cmake -G Ninja -S "zlib-${pkgver}" -B build-$_arch
    cmake --build build-$_arch
  done
}

package () {
  for _arch in ${_architectures}; do
    source mingw-clang-env $_arch
    DESTDIR=$pkgdir cmake --install build-$_arch
    $STRIP -x -g "${pkgdir}/usr/${_arch}/bin/"*.dll
    $STRIP -g "${pkgdir}/usr/${_arch}/lib/"*.a
    ln -srv "$pkgdir/usr/$_arch/lib/libzlib.dll.a" "$pkgdir/usr/$_arch/lib/libz.dll.a"
    ln -srv "$pkgdir/usr/$_arch/lib/libzlibstatic.a" "$pkgdir/usr/$_arch/lib/libz.a"
    rm -vr "$pkgdir/usr/$_arch/share/man"
  done
}
